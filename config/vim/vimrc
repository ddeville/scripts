" vim needs a posix shell and fish is not
if &shell =~# "fish$"
    set shell=bash
endif

set encoding=utf-8

" store swap and backup files under `~/.vim/backup`
if !isdirectory($HOME . "/.vim/cache/backup")
    call mkdir($HOME . "/.vim/cache/backup", "p", 0700)
endif
set directory=$HOME/.vim/cache/backup//
set backupdir=$HOME/.vim/cache/backup//

" permanent undo
if !isdirectory($HOME . "/.vim/cache/undo")
    call mkdir($HOME . "/.vim/cache/undo", "p", 0700)
endif
set undodir=$HOME/.vim/cache/undo
set undofile

" set leader to space instead of the default backslash
noremap <Space> <Nop>
let mapleader = "\<Space>"

" attempt to determine the file type based on name and content
filetype plugin indent on

" vim-plug plugins
call plug#begin("~/.vim-plug-plugins")
    " VIM & NEOVIM
    Plug 'chriskempson/base16-vim'
    Plug 'dag/vim-fish'
    Plug 'fatih/vim-go'
        " we use nvim-lsp instead
        let g:go_gopls_enabled = 0
        let g:go_code_completion_enabled = 0
        let g:go_def_mapping_enabled = 0
        let g:go_diagnostics_enabled = 0
        let g:go_echo_go_info = 0
        let g:go_metalinter_enabled = 0
        let g:go_fmt_autosave = 1
    Plug 'itchyny/lightline.vim'
        " don't show `-- INSERT --` since we have a fancy status line
        set noshowmode
        " update color scheme
        let g:lightline =
        \ {
        \   "colorscheme": "wombat",
        \   "active": {
        \     "left": [["mode", "paste"], ["gitbranch", "readonly", "filename", "modified"]]
        \   },
        \   "component_function": {
        \     "gitbranch": "fugitive#head"
        \   },
        \ }
    Plug 'junegunn/fzf', {'dir': '~/.fzf', 'do': './install --bin'}
    Plug 'junegunn/fzf.vim'
        let g:fzf_layout = { "down": "35%" }
        " we use different commands in the shell
        let $FZF_DEFAULT_COMMAND = "fd --type file --color=never --hidden --exclude .git"
        nnoremap <Leader>; :Buffers<CR>
        nnoremap ; :Buffers<CR>
        nnoremap <Leader>o :Files<CR>
        nnoremap <C-p> :Files<CR>
        nnoremap <Leader>s :GFiles?<CR>
        nnoremap <Leader>t :Tags<CR>
        nnoremap // :BLines<CR>
    Plug 'machakann/vim-highlightedyank'
        let g:highlightedyank_highlight_duration = -1
    Plug 'mhinz/vim-grepper'
        let g:grepper = {}
        let g:grepper.tools = ["rg", "git", "grep"]
        let g:grepper.highlight = 1
        nnoremap <Leader>f :Grepper -tool rg<CR>
        nmap gs <plug>(GrepperOperator)
        xmap gs <plug>(GrepperOperator)
    Plug 'mhinz/vim-startify'
    Plug 'rust-lang/rust.vim'
        let g:rustfmt_autosave = 1
        let g:rustfmt_emit_files = 1
        let g:rustfmt_fail_silently = 0
    Plug 'tpope/vim-commentary'
    Plug 'tpope/vim-eunuch'
    Plug 'tpope/vim-fugitive'
    Plug 'tpope/vim-git'
    Plug 'tpope/vim-repeat'
    Plug 'tpope/vim-sleuth'
    Plug 'tpope/vim-surround'
    Plug 'tpope/vim-unimpaired'
    Plug 'tpope/vim-vinegar'
    Plug 'vifm/vifm.vim'

    " NEOVIM ONLY
if has("nvim")
    Plug 'hrsh7th/nvim-compe' | Plug 'hrsh7th/vim-vsnip'
    Plug 'neovim/nvim-lspconfig'
    Plug 'nvim-telescope/telescope.nvim' | Plug 'nvim-lua/popup.nvim' | Plug 'nvim-lua/plenary.nvim'
    Plug 'nvim-treesitter/nvim-treesitter', {'do': ':TSUpdate'}
endif
call plug#end()

" update and reload config
command! Config execute ":edit $MYVIMRC"
command! Reload execute "source ~/.vim/vimrc"

" enable syntax highlighting
syntax enable

" dark background is better
set background=dark

" setup base16 colorscheme
if filereadable(expand("~/.vimrc_background"))
    let base16colorspace = 256
    source ~/.vimrc_background
endif

" shorten the delay to transition to normal mode after pressing escape
set timeoutlen=1000 ttimeoutlen=0

" allow switching between buffers without saving first
set hidden

" use 4 spaces instead of tabs
set shiftwidth=4
set softtabstop=4
set tabstop=4
set expandtab

" set mouse support so that we can select text and tabs
if has("mouse")
    set mouse=a
endif

" if no known file type keep the same indent as the current line
set autoindent

" backspace through anything in insert mode
set backspace=indent,eol,start

" highlight search results and start searching immediately
set hlsearch
set incsearch

" always show at least one line above/below the cursor (and 5 on each side if not wrapping)
set scrolloff=1
set sidescrolloff=5

" display the cursor position in the status line
set ruler

" display the line number on the left
set number

" show the diagnostic ui where the number goes
if has("nvim-0.5") || has("patch-8.1.1564")
    set signcolumn=number
else
    set signcolumn=yes
end

" display a column at 80 char
set colorcolumn=80

" highlight the current line
set cursorline

" always show the status line even when there is only one window
set laststatus=2

" autoload file changes
set autoread

" open new splits on right and bottom
set splitbelow
set splitright

" don't add an extra space when joining lines
set nojoinspaces

" no beeping
set vb t_vb=

" don't give |ins-completion-menu| messages.
set shortmess+=c

" trailing whitespace is bad
highlight WhiteSpaceEOL guibg=Red ctermbg=Red
autocmd InsertEnter * match WhiteSpaceEOL /\s\+\%#\@<!$/
autocmd InsertLeave * match WhiteSpaceEOL /\s\+$/

" netrw
let g:netrw_localrmdir = "rm -r"
let g:netrw_liststyle = 1
let g:netrw_winsize = 85
let g:netrw_browse_split = 0

" search current selection in visual mode with */#
function! s:VSetSearch()
    let temp = @@
    norm! gvy
    let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
    let @@ = temp
endfunction
vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR><c-o>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR><c-o>

" ==> MAPPINGS

" disable arrow keys
map <up> <nop>
map <down> <nop>
map <left> <nop>
map <right> <nop>
imap <up> <nop>
imap <down> <nop>
imap <left> <nop>
imap <right> <nop>

" map screen redraw to also turn off search highlighting
nnoremap <C-l> :nohl<CR><C-l>

" move lines up and down when in visual mode
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv

" ==> DIAGNOSTICS

" diagnostic error messages in red
highlight LspDiagnosticsDefaultError guifg=Red ctermfg=Red
highlight LspDiagnosticsVirtualTextError guifg=Red ctermfg=Red
highlight LspDiagnosticsSignError guifg=Red ctermfg=Red
highlight LspDiagnosticsUnderlineError guifg=Red ctermfg=Red
" diagnostic warning messages in yellow
highlight LspDiagnosticsDefaultWarning guifg=Yellow ctermfg=Yellow
highlight LspDiagnosticsVirtualTextWarning guifg=Yellow ctermfg=Yellow
highlight LspDiagnosticsSignWarning guifg=Yellow ctermfg=Yellow
highlight LspDiagnosticsUnderlineWarning guifg=Red ctermfg=Red
" diagnostic info messages in white
highlight LspDiagnosticsDefaultInformation guifg=White ctermfg=White
highlight LspDiagnosticsVirtualTextInformation guifg=White ctermfg=White
highlight LspDiagnosticsSignInformation guifg=White ctermfg=White
highlight LspDiagnosticsUnderlineInformation guifg=White ctermfg=White
" diagnostic hint messages in gray
highlight LspDiagnosticsDefaultHint guifg=Gray ctermfg=Gray
highlight LspDiagnosticsVirtualTextHint guifg=Gray ctermfg=Gray
highlight LspDiagnosticsSignHint guifg=Gray ctermfg=Gray
highlight LspDiagnosticsUnderlineHint guifg=Gray ctermfg=Gray

" configure sign column for diagnostic messages
sign define LspDiagnosticsSignError text=E texthl=LspDiagnosticsDefaultError
sign define LspDiagnosticsSignWarning text=W texthl=LspDiagnosticsDefaultWarning
sign define LspDiagnosticsSignInformation text=I texthl=LspDiagnosticsDefaultInformation
sign define LspDiagnosticsSignHint text=H texthl=LspDiagnosticsDefaultHint

" ==> LUA

" load more configs written in lua (for neovim only)
if has("nvim")
lua require("config")
endif
